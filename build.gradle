/*
 * Copyright (c) 2018.
 *
 * This file is part of ProcessManager.
 *
 * ProcessManager is free software: you can redistribute it and/or modify it under the terms of version 3 of the
 * GNU Lesser General Public License as published by the Free Software Foundation.
 *
 * ProcessManager is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even
 * the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License along with ProcessManager.  If not,
 * see <http://www.gnu.org/licenses/>.
 */


ext {
    if (!rootProject.hasProperty("androidCompatVersion")) {
        androidCompatVersion = "28.0.0"
    }
}

buildscript {
    ext {
        if (!rootProject.hasProperty("androidPluginVersion")) {
            androidPluginVersion = "3.3.2"
        }
        if (!rootProject.hasProperty("kotlin_version")) {
            kotlin_version = "1.3.30"
        }
    }
    repositories {
        mavenLocal()
        jcenter()
        google()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${androidPluginVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

version = "1.2.6-SNAPSHOT"
description = "Authentication library providing accounts that link against android."

dependencies {
    implementation "com.android.support:support-annotations:${androidCompatVersion}"
    implementation "com.android.support:recyclerview-v7:${androidCompatVersion}"
    implementation "com.android.support:appcompat-v7:${androidCompatVersion}"
    implementation "com.android.support:cardview-v7:${androidCompatVersion}"
    implementation "com.android.support.constraint:constraint-layout:1.1.3"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.esotericsoftware:kryo:4.0.2"
    implementation "net.devrieze:android-coroutines-appcompat:0.7.1-SNAPSHOT"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.0"
    implementation "android.arch.lifecycle:extensions:1.1.1"
//    kapt "com.android.databinding:compiler:$androidPluginVersion"
    kapt "android.arch.lifecycle:compiler:1.1.1"
}

repositories {
    jcenter()
    google()
    mavenLocal()
}

android {
    compileSdkVersion(28)
    defaultConfig {
        applicationId="uk.ac.bournemouth.darwin.auth"
        minSdkVersion(16)
        targetSdkVersion(28)
        versionCode=1002005
        versionName=version
        resValue("string", "applicationId", applicationId)
    }
    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            applicationIdSuffix=".debug"
            versionNameSuffix="-debug"
            resValue("string", "applicationId", android.defaultConfig.applicationId + applicationIdSuffix)
        }
        debugMinified {
            applicationIdSuffix=".debugmini"
            versionNameSuffix="-debugmini"
            resValue "string", "applicationId", android.defaultConfig.applicationId + applicationIdSuffix

            minifyEnabled=true
            shrinkResources=true
            proguardFiles(getDefaultProguardFile("proguard-android.txt"),
                    "proguard-project.txt")
        }
        release {
//            signingConfig signingConfigs.releaseConfig
            minifyEnabled=true
            shrinkResources=true
            proguardFiles(getDefaultProguardFile("proguard-android.txt"),
                    "proguard-project.txt")
        }
    }
    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude("META-INF/*.kotlin_module")
    }
}


androidExtensions {
    experimental = true
}
